{
 "cells": [
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "\"Composite type to hold ambient conditions, parameters and initial conditions\"\n",
    "struct AmbientConditions\n",
    "    \"Ambient velocity [m/s]\"\n",
    "    u_a::AbstractFloat\n",
    "    \"Ambient temperature [K]\"\n",
    "    T_a::AbstractFloat\n",
    "    \"Ambient passive tracer mixing ratio\"\n",
    "    χ_a::AbstractFloat\n",
    "    \"Prandtl number\"\n",
    "    Pr::AbstractFloat\n",
    "    \"Lewis number\"\n",
    "    Le::AbstractFloat\n",
    "    \"Specific gas constant of air [J/kg]\"\n",
    "    R::AbstractFloat\n",
    "    \"Ambient pressure [Pa]\"\n",
    "    p::AbstractFloat\n",
    "    \"Initial temperature [K]\"\n",
    "    T0::AbstractFloat\n",
    "    \"Initial velocity\"\n",
    "    u0::AbstractFloat\n",
    "    \"Initial passive tracer mixing ratio\"\n",
    "    χ0::AbstractFloat\n",
    "end\n",
    "\n",
    "\"\"\"\n",
    "    second_order_central(x, i, Δ)\n",
    "Computes the second order central difference approximation to the second\n",
    "derivative of the variable x w.r.t. its discretization dimension (e.g. x is\n",
    "the state, and its entries x[i] are the values for different radial dimensions)\n",
    "using the step size Δ, at location i.\n",
    "# Inputs\n",
    "- `x` State vector\n",
    "- `i` Index to compute second order difference for \n",
    "- 'Δ' The spacing between the vector elements\n",
    "\"\"\"\n",
    "function second_order_central(x::AbstractVector, i::Integer, Δ::AbstractFloat)\n",
    "    return ((x[i+1] - 2 * x[i] + x[i-1]) / Δ^2)\n",
    "end\n",
    "\n",
    "\"\"\"\n",
    "    construct_rhs(u, T, y, Δψ, Δϕ, ambient; χ=nothing)\n",
    "Constructs the right-hand-side vector for the system `Ax=b` resulting\n",
    "from applying the Crank-Nicholson scheme to the governing equations.\n",
    "# Inputs \n",
    "- `u` axial velocities\n",
    "- `T` temperature\n",
    "- `y` y-locations of grid points \n",
    "- `Δψ` Spacing between grid points in ϕ-ψ space\n",
    "- `Δϕ` Step size in ϕ-ψ space\n",
    "- `ambient` Ambient conditions\n",
    "- `χ` Mixing ratio of passive tracer\n",
    "\"\"\"\n",
    "function construct_rhs(u::AbstractVector, T::AbstractVector, y::AbstractVector,\n",
    "    Δψ::AbstractVector, Δϕ::AbstractFloat, ambient::AmbientConditions;\n",
    "    χ::AbstractVector=nothing)\n",
    "\n",
    "    if χ !== nothing\n",
    "        b = zeros(size(u)[1] + size(T)[1] + size(χ)[1])\n",
    "        include_χ = true\n",
    "    else\n",
    "        b = zeros(size(u)[1] + size(T)[1])\n",
    "        include_χ = false\n",
    "    end\n",
    "\n",
    "    ## Boundary conditions\n",
    "\n",
    "    # Neumann conditions at y=0\n",
    "    b[1] = 0.0\n",
    "    b[size(u)[1]+1] = 0.0\n",
    "\n",
    "    # Dirichlet conditions at y -> infinity \n",
    "    b[size(u)[1]] = ambient.u_a\n",
    "    b[size(u)[1]+size(T)[1]] = ambient.T_a\n",
    "\n",
    "    # Definition for convenience \n",
    "    λ = ambient.u0 / ((ambient.R / ambient.p)^2 * ambient.T0^2)\n",
    "    Le = ambient.Le\n",
    "\n",
    "    # Loop through b \n",
    "    for i = 2:size(u)[1]-1\n",
    "        b[i] = u[i] / Δϕ + 0.5 * λ * y[i]^2 * second_order_central(u, i, Δψ[i])\n",
    "        b[i+size(u)[1]] = (T[i] / Δϕ + 0.5 * λ * y[i]^2\n",
    "                                       * second_order_central(T, i, Δψ[i]) / ambient.Pr)\n",
    "    end\n",
    "\n",
    "    if include_χ\n",
    "        for i = 2:size(u)[1]-1\n",
    "            b[i+2*size(u)[1]] = (χ[i] / Δϕ + Le * 0.5 * λ * y[i]^2\n",
    "                                             * second_order_central(χ, i, Δψ[i]) / ambient.Pr)\n",
    "        end\n",
    "        b[size(u)[1]+size(T)[1]+1] = 0.0\n",
    "        b[end] = ambient.χ_a\n",
    "    end\n",
    "\n",
    "    return b\n",
    "end\n",
    "\n",
    "\"\"\"\n",
    "    construct_tridiagonal_matrix(n, Δψ, Δϕ, y, ambient; include_χ=false)\n",
    "Constructs the matrix `A` for the system `Ax=b` resulting\n",
    "from applying the Crank-Nicholson scheme to the governing equations.\n",
    "# Inputs \n",
    "- `n` Number of grid points in radial direction\n",
    "- `Δψ` Spacing between grid points in ϕ-ψ space\n",
    "- `Δϕ` Step size in ϕ-ψ space\n",
    "- `y` y-locations of grid points \n",
    "- `ambient` Ambient conditions\n",
    "- `include_χ` indicates if passive tracer is simulated as well\n",
    "\"\"\"\n",
    "function construct_tridiagonal_matrix(n::Integer, Δψ::AbstractVector,\n",
    "    Δϕ::AbstractFloat, y::AbstractVector, ambient::AmbientConditions;\n",
    "    include_χ::Bool=false)\n",
    "\n",
    "    if include_χ\n",
    "        A = zeros((3 * n, 3 * n))\n",
    "    else\n",
    "        A = zeros((2 * n, 2 * n))\n",
    "    end\n",
    "    ## Boundary conditions \n",
    "\n",
    "    # Neumann conditions at y=0\n",
    "    A[1, 1] = (-1 / (Δψ[1]))\n",
    "    A[1, 2] = 1 / (Δψ[1])\n",
    "    A[n+1, n+1] = -1 / Δψ[1]\n",
    "    A[n+1, n+2] = 1 / Δψ[1]\n",
    "\n",
    "    # Dirichlet conditions at y -> infinity \n",
    "    A[n, n] = 1.0\n",
    "    A[2*n, 2*n] = 1.0\n",
    "\n",
    "    # Definition for convenience \n",
    "    λ = ambient.u0 / ((ambient.R / ambient.p)^2 * ambient.T0^2)\n",
    "\n",
    "    Pr = ambient.Pr\n",
    "    Le = ambient.Le\n",
    "    # Loop through matrix \n",
    "    for i = 2:n-1\n",
    "\n",
    "        # For u \n",
    "        A[i, i] = 1 / Δϕ + λ * y[i]^2 / (Δψ[i]^2)\n",
    "        A[i, i-1] = -λ * y[i]^2 / (2 * Δψ[i]^2)\n",
    "        A[i, i+1] = -λ * y[i]^2 / (2 * Δψ[i]^2)\n",
    "\n",
    "        # For T \n",
    "        j = i + n\n",
    "        A[j, j] = 1 / Δϕ + λ * y[i]^2 / (Pr * Δψ[i]^2)\n",
    "        A[j, j-1] = -λ * y[i]^2 / (2 * Pr * Δψ[i]^2)\n",
    "        A[j, j+1] = -λ * y[i]^2 / (2 * Pr * Δψ[i]^2)\n",
    "    end\n",
    "\n",
    "    if include_χ\n",
    "        A[2*n+1:end, 2*n+1:end] = Le .* A[n+1:2*n, n+1:2*n]\n",
    "        A[2*n+1, 2*n+1] = -1 / Δψ[1]\n",
    "        A[2*n+1, 2*n+2] = 1 / Δψ[1]\n",
    "        A[3*n, 3*n] = 1.0\n",
    "    end\n",
    "\n",
    "    return A\n",
    "end\n",
    "\n",
    "\"\"\"\n",
    "    compute_y(u, T, Δψ, R, p)\n",
    "Computes y locations corresponding to particular states, based on the \n",
    "grid spacing in ϕ-ψ space. \n",
    "# Inputs \n",
    "- `u` axial velocities\n",
    "- `T` temperature\n",
    "- `Δψ` Spacing in ϕ-ψ space\n",
    "- `R` Specific gas constant for air in J/kg\n",
    "- `p` Ambient pressure in Pa\n",
    "\"\"\"\n",
    "function compute_y(u::AbstractVector, T::AbstractVector, Δψ::AbstractVector,\n",
    "    R::AbstractFloat, p::AbstractFloat)\n",
    "    return (2 * cumsum((R / p) * T ./ u) .* Δψ) .^ 0.5\n",
    "\n",
    "end\n",
    "\n",
    "\"\"\"\n",
    "   compute_x(ϵ, Δϕ)\n",
    "Computes x locations of the grid based on turbulent mixing coefficient ϵ\n",
    "and step sizes Δϕ\n",
    "# Inputs \n",
    "- `ϵ` Values of the turbulent mixing coefficient\n",
    "- `Δϕ` Step sizes\n",
    "\"\"\"\n",
    "function compute_x(ϵ::AbstractVector, Δϕ::AbstractVector)\n",
    "    x = zeros(size(ϵ))\n",
    "    x[2:end] = cumsum(Δϕ[1:end-1] ./ ϵ[1:end-1])\n",
    "    return x\n",
    "end\n",
    "\n",
    "\"\"\"\n",
    "    compute_ψ(u, T, y, p, R)\n",
    "Computes the transformed coordinate ψ based on the state and y locations.\n",
    "# Inputs\n",
    "- `u` axial velocities\n",
    "- `T` temperature\n",
    "- `y` y-locations of grid points \n",
    "- `R` Specific gas constant for air in J/kg\n",
    "- `p` Ambient pressure in Pa\n",
    "\"\"\"\n",
    "function compute_ψ(u::AbstractVector, T::AbstractVector, y::AbstractVector,\n",
    "    p::AbstractFloat, R::AbstractFloat)\n",
    "    return cumsum(p ./ (R * T) .* (y[2:end] .- y[1:end-1]) .* u)\n",
    "end\n",
    "\n",
    "\"\"\"\n",
    "    get_ϵ(κ, u, y)\n",
    "Computes the turbulent mixng coefficient based on current velocity profile.\n",
    "# Inputs\n",
    "- `κ` Empirical parameter\n",
    "- `u` axial velocities\n",
    "- `y` y-locations of grid points \n",
    "\"\"\"\n",
    "function get_ϵ(κ::AbstractFloat, u::AbstractVector, y::AbstractVector)\n",
    "    # First find the half-width \n",
    "    fractional_change = (u .- u[end]) ./ (u[1] - u[end])\n",
    "    idx = argmin(abs.(fractional_change .- 0.5))\n",
    "    half_width = y[idx]\n",
    "    return κ * (u[1] - u[end]) * half_width\n",
    "end\n",
    "\n",
    "\"\"\"\n",
    "    solve_exhaust_flow(u_init, T_init, ambient, n, Δϕ, Δψ; χ_init=nothing)\n",
    "Solves for the velocity, temperature and optionally passive tracer profiles\n",
    "within an aircraft engine exhaust jet.\n",
    "# Inputs\n",
    "- `u_init` Initial velocity profile\n",
    "- `T_init` Initial temperature profile\n",
    "- `ambient` AmbientConditions struct\n",
    "- `n` Number of grid points in the radial direction\n",
    "- `Δϕ` Grid spacing in ϕ-ψ space.\n",
    "- `Δψ` Grid spacing in ϕ-ψ space.\n",
    "- `χ_init` Initial passive tracer profile.\n",
    "\"\"\"\n",
    "function solve_exhaust_flow(u_init::AbstractVector, T_init::AbstractVector,\n",
    "    ambient::AmbientConditions, n::Integer, Δϕ::AbstractVector,\n",
    "    Δψ::AbstractVector; χ_init::AbstractVector=nothing)\n",
    "\n",
    "    u_mem = zeros((size(u_init)[1], n))\n",
    "    T_mem = zeros((size(u_init)[1], n))\n",
    "    y_mem = zeros((size(u_init)[1], n))\n",
    "    u_mem[:, 1] = u_init\n",
    "    T_mem[:, 1] = T_init\n",
    "    ϵ = zeros(n)\n",
    "\n",
    "    include_χ = true\n",
    "    if χ_init !== nothing\n",
    "        include_χ = true\n",
    "        χ_mem = zeros((size(χ_init)[1], n))\n",
    "        χ_mem[:, 1] = χ_init\n",
    "    end\n",
    "\n",
    "    @inbounds for i = 1:n-1\n",
    "        y_mem[:, i] = compute_y(@view(u_mem[:, i]), @view(T_mem[:, i]), Δψ, ambient.R, ambient.p)\n",
    "\n",
    "        if include_χ\n",
    "            A = construct_tridiagonal_matrix(size(u_init)[1], Δψ, Δϕ[i],\n",
    "                @view(y_mem[:, i]), ambient, include_χ=include_χ)\n",
    "            b = construct_rhs(@view(u_mem[:, i]), @view(T_mem[:, i]),\n",
    "                @view(y_mem[:, i]), Δψ, Δϕ[i], ambient, χ=@view(χ_mem[:, i]))\n",
    "            sol = A \\ b\n",
    "            u_mem[:, i+1] = sol[1:size(u_init)[1]]\n",
    "            T_mem[:, i+1] = sol[size(u_init)[1]+1:size(u_init)[1]+size(T_init)[1]]\n",
    "            χ_mem[:, i+1] = sol[size(u_init)[1]+size(T_init)[1]+1:end]\n",
    "        else\n",
    "            A = construct_tridiagonal_matrix(size(u_init)[1], Δψ, Δϕ[i], @view(y_mem[:, i]), ambient)\n",
    "            b = construct_rhs(@view(u_mem[:, i]), @view(T_mem[:, i]), @view(y_mem[:, i]), Δψ, Δϕ[i], ambient)\n",
    "            sol = A \\ b\n",
    "            u_mem[:, i+1] = sol[1:size(u_init)[1]]\n",
    "            T_mem[:, i+1] = sol[size(u_init)[1]+1:end]\n",
    "        end\n",
    "        ϵ[i] = get_ϵ(0.02, @view(u_mem[:, i]), @view(y_mem[:, i]))\n",
    "    end\n",
    "\n",
    "    x = compute_x(ϵ, Δϕ)\n",
    "    if include_χ\n",
    "        return x, y_mem, u_mem, T_mem, χ_mem, ϵ\n",
    "    else\n",
    "        return x, y_mem, u_mem, T_mem\n",
    "    end\n",
    "end\n",
    "\n",
    "\"\"\"\n",
    "    regrid_solution(x, y, u, T, χ, y_spacing)\n",
    "Maps back solution from a grid in ϕ-ψ space to a grid in x-y space.\n",
    "# Inputs\n",
    "- `x` x coordinates corresponding to the grid\n",
    "- `y` y coordinates corresponding to the grid\n",
    "- `u` Velocity values of solution\n",
    "- `T` Temperature values of solution\n",
    "- `y_spacing` Desired spacing in `y` direction for output grid\n",
    "\"\"\"\n",
    "function regrid_solution(x::AbstractVector, y::AbstractMatrix, u::AbstractMatrix,\n",
    "    T::AbstractMatrix, χ::AbstractMatrix, y_spacing::AbstractFloat)\n",
    "\n",
    "    yy = 0:y_spacing:maximum(y)\n",
    "\n",
    "    u_gridded = zeros((size(yy)[1], size(x)[1]))\n",
    "    T_gridded = zeros((size(yy)[1], size(x)[1]))\n",
    "    χ_gridded = zeros((size(yy)[1]), size(x)[1])\n",
    "\n",
    "    for j = 1:size(x)[1]\n",
    "        u_interp_extrap = LinearInterpolation(y[:, j], u[:, j], extrapolation_bc=Line())\n",
    "        u_gridded[:, j] = u_interp_extrap(yy)\n",
    "        T_interp_extrap = LinearInterpolation(y[:, j], T[:, j], extrapolation_bc=Line())\n",
    "        T_gridded[:, j] = T_interp_extrap(yy)\n",
    "        χ_interp_extrap = LinearInterpolation(y[:, j], χ[:, j], extrapolation_bc=Line())\n",
    "        χ_gridded[:, j] = χ_interp_extrap(yy)\n",
    "    end\n",
    "\n",
    "    return x, yy, u_gridded, T_gridded, χ_gridded\n",
    "end\n",
    "\n",
    "function show_profiles(x, y, var, cols)\n",
    "    labels = [\"x = $xx m\" for xx in x[cols]]\n",
    "    plot(var[:, cols], y[:, cols], dpi=300, label=permutedims(labels))\n",
    "end\n",
    "\n",
    "\"\"\"\n",
    "    plot_heatmap(x, y, var, xlabel, ylabel, clabel, colormap)\n",
    "Plots gridded solution. \n",
    "# Inputs\n",
    "- `x` x location of grid points\n",
    "- `y` y location of grid points\n",
    "- `var` Solution variable values at grid points\n",
    "- `xlabel` Label for horizontal axis\n",
    "- `ylabel` Label for vertical axis\n",
    "- `clabel` Label for colormap\n",
    "- `colormap` Colormap to use (e.g. :viridis)\n",
    "\"\"\"\n",
    "function plot_heatmap(x, y, var, xlabel, ylabel, clabel, colormap, x_max)\n",
    "    # This is kind of hard-coded, to avoid any issues with the first entry of\n",
    "    # `x` which typically is zero. \n",
    "\n",
    "    y_plot = 20.0 .* range(0, stop=1, length=size(y)[1])\n",
    "    y_len = length(y)\n",
    "    u = 0\n",
    "    x_lim = 0\n",
    "\n",
    "    for g in 1:length(x)\n",
    "        if x[g] >= x_max && u == 0#desired xlim\n",
    "            x_lim = g\n",
    "            u = 1\n",
    "        end\n",
    "    end\n",
    "\n",
    "    heatmap((x[2:x_lim]), y, clim=(0, Inf),\n",
    "        var[1:y_len, 2:x_lim], colorbar_title=clabel, size=(700, 500), dpi=300, c=colormap)\n",
    "    xlabel!(xlabel)\n",
    "    ylabel!(ylabel)\n",
    "    #xticks!([-2, -1, 0, 1, 2, 3], [\"0.1\", \"1\", \"10\", \"100\",])\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "struct AmbientConditionsχ\n",
    "    \"Ambient velocity [m/s]\"\n",
    "    u_a::AbstractFloat\n",
    "    \"Ambient temperature [K]\"\n",
    "    T_a::AbstractFloat\n",
    "    \"Ambient passive tracer mixing ratio\"\n",
    "    χ_a::Array\n",
    "    \"Prandtl number\"\n",
    "    Pr::AbstractFloat\n",
    "    \"Lewis number\"\n",
    "    Le::AbstractFloat\n",
    "    \"Specific gas constant of air [J/kg]\"\n",
    "    R::AbstractFloat\n",
    "    \"Ambient pressure [Pa]\"\n",
    "    p::AbstractFloat\n",
    "    \"Initial temperature [K]\"\n",
    "    T0::AbstractFloat\n",
    "    \"Initial velocity\"\n",
    "    u0::AbstractFloat\n",
    "    \"Initial passive tracer mixing ratio\"\n",
    "    χ0::Array\n",
    "end\n",
    "\n",
    "\n",
    "function solve_reaction(χ_h0, T, Δϕ, ϵ, u, gas, j, χ_1, s, n_species, gas_prop)\n",
    "    #temp drops but should have ambient amounts instead of initial amounts\n",
    "    P = 101325 #Pa\n",
    "    ω = zeros(size(χ_h0))\n",
    "\n",
    "    reactor = ct.IdealGasConstPressureReactor(gas)\n",
    "\n",
    "    #FOR NO REACTIONS\n",
    "    gas.set_multiplier(0)\n",
    "\n",
    "    states = ct.SolutionArray(gas)\n",
    "\n",
    "    for i = 1:length(χ_h0[:, 1]) #index through all \"y\"s\n",
    "\n",
    "        try\n",
    "            gas.TPX = T[i], P, χ_h0[i, :] #GO THROUGH TO SEE IF SETTING AND UPDATING GAS CORRECTLY\n",
    "        catch\n",
    "            println(i)\n",
    "            print(gas.report())\n",
    "        end\n",
    "\n",
    "        reactor.syncState()\n",
    "        reactorNet = ct.ReactorNet([reactor])\n",
    "        t_final = Δϕ / (u[i] * abs(ϵ))\n",
    "        t = 0\n",
    "\n",
    "        reactorNet.advance(t_final, apply_limit=false)\n",
    "\n",
    "        states.append(reactor.thermo.state)\n",
    "\n",
    "        χ_1[i, :] = 10^6 .* reactor.thermo.X #mole fraction to ppm state.X[len,:] 10^9 #kmol/m^3s, assume 1 m^3, to ppm #rates for specific y (i) and all species UNITS\n",
    "\n",
    "    end\n",
    "\n",
    "    return χ_1, states #for all y and all species [s,n_species]\n",
    "\n",
    "end\n",
    "\n",
    "function construct_tridiagonal_matrix_χ(n::Integer, Δψ::AbstractVector,\n",
    "    Δϕ::AbstractFloat, y::AbstractVector, ambient::AmbientConditionsχ)\n",
    "\n",
    "    A = zeros((n, n))\n",
    "\n",
    "    ## Boundary conditions \n",
    "    # Neumann conditions at y=0 #change\n",
    "    A[1, 1] = -1 / (Δψ[1])\n",
    "    A[1, 2] = 1 / (Δψ[1])\n",
    "\n",
    "    # Dirichlet conditions at y -> infinity #change\n",
    "    A[n, n] = 1.0\n",
    "\n",
    "    # Definition for convenience\n",
    "    Pr = ambient.Pr\n",
    "    Le = ambient.Le\n",
    "    λ = (Le / Pr) * ((ambient.u0 * ambient.p^2) / ((ambient.R * ambient.T0)^2))\n",
    "\n",
    "    # Loop through matrix \n",
    "    for j = 2:n-1\n",
    "        # For χ\n",
    "        A[j, j] = 1 / Δϕ + λ * y[j]^2 * 2 / (Δψ[j]^2)\n",
    "        A[j, j-1] = -λ * y[j]^2 / (Δψ[j]^2)\n",
    "        A[j, j+1] = -λ * y[j]^2 / (Δψ[j]^2)\n",
    "    end\n",
    "    return A\n",
    "end\n",
    "\n",
    "function construct_rhs_χ(u, T, y, Δψ, Δϕ, ambient::AmbientConditionsχ, χ, j)\n",
    "\n",
    "    b = zeros(size(χ)[1])\n",
    "\n",
    "    ## Boundary conditions\n",
    "    # Neumann conditions at y=0 #change\n",
    "    b[1] = 0.0\n",
    "\n",
    "    # Dirichlet conditions at y -> infinity #change\n",
    "    b[size(χ)[1]] = ambient.χ_a[j]\n",
    "\n",
    "    # Definition for convenience \n",
    "    Pr = ambient.Pr\n",
    "    Le = ambient.Le\n",
    "    λ = (Le / Pr) * ((ambient.u0 * ambient.p^2) / ((ambient.R * ambient.T0)^2))\n",
    "\n",
    "    # Loop through b\n",
    "    for i = 2:size(χ)[1]-1\n",
    "        b[i] = χ[i] / Δϕ + λ * y[i]^2 * second_order_central(χ, i, Δψ[i])\n",
    "        #took out 0.5 factor on lambda (not 100% sure why was there) ^^^\n",
    "    end\n",
    "\n",
    "    return b\n",
    "end\n",
    "\n",
    "function solve_exhaust_flow_χ(u_mem, T_mem, ambient::AmbientConditionsχ, n::Integer,\n",
    "    Δϕ, Δψ::AbstractVector, χ_init, i, j)\n",
    "\n",
    "    y_mem = zeros(size(u_mem))\n",
    "\n",
    "    χ_mem = χ_init\n",
    "\n",
    "    y_mem = compute_y(u_mem, T_mem, Δψ, ambient.R, ambient.p)\n",
    "\n",
    "    A = construct_tridiagonal_matrix_χ(size(u_mem)[1], Δψ, Δϕ, y_mem, ambient)\n",
    "    \n",
    "    b = construct_rhs_χ(u_mem, T_mem, y_mem, Δψ, Δϕ, ambient, χ_mem, j)\n",
    "\n",
    "    sol = A \\ b\n",
    "\n",
    "    χ_mem = sol\n",
    "\n",
    "    return χ_mem\n",
    "end\n",
    "\n",
    "struct gas_type\n",
    "    gas\n",
    "end"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.5.1",
   "language": "julia",
   "name": "julia-1.5"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.5.1"
  },
  "orig_nbformat": 4
 },
 "nbformat": 4,
 "nbformat_minor": 2
}
